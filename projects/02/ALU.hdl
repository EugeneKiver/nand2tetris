// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/02/ALU.hdl

/**
 * The ALU (Arithmetic Logic Unit).
 * Computes one of the following functions:
 * x+y, x-y, y-x, 0, 1, -1, x, y, -x, -y, !x, !y,
 * x+1, y+1, x-1, y-1, x&y, x|y on two 16-bit inputs, 
 * according to 6 input bits denoted zx,nx,zy,ny,f,no.
 * In addition, the ALU computes two 1-bit outputs:
 * if the ALU output == 0, zr is set to 1; otherwise zr is set to 0;
 * if the ALU output < 0, ng is set to 1; otherwise ng is set to 0.
 */

// Implementation: the ALU logic manipulates the x and y inputs
// and operates on the resulting values, as follows:
// if (zx == 1) set x = 0        // 16-bit constant
// if (nx == 1) set x = !x       // bitwise not
// if (zy == 1) set y = 0        // 16-bit constant
// if (ny == 1) set y = !y       // bitwise not
// if (f == 1)  set out = x + y  // integer 2's complement addition
// if (f == 0)  set out = x & y  // bitwise and
// if (no == 1) set out = !out   // bitwise not
// if (out == 0) set zr = 1
// if (out < 0) set ng = 1

// And16 Not16 Or16 Mux16 | Add16 Inc16 | DMux4Way DMux8Way Or8Way| Mux4Way16 Mux8Way16
CHIP ALU {
    IN  
        x[16], y[16],  // 16-bit inputs        
        zx, // zero the x input?
        nx, // negate the x input?
        zy, // zero the y input?	
        ny, // negate the y input?
        f,  // compute out = x + y (if 1) or x & y (if 0)
        no; // negate the out output?

    OUT 
        out[16], // 16-bit output
        zr, // 1 if (out == 0), 0 otherwise
        ng; // 1 if (out < 0),  0 otherwise

    PARTS:
	//prepare x
	Mux16(a=x, b=false, sel=zx, out=mux-zx);
	Not16(in=mux-zx, out=n-zx);
	Mux16(a=mux-zx, b=n-zx, sel=nx, out=x-ready);
	
	//prepare y
	Mux16(a=y, b=false, sel=zy, out=mux-zy);
	Not16(in=mux-zy, out=n-zy);
	Mux16(a=mux-zy, b=n-zy, sel=ny, out=y-ready);
	
	// function
	Add16(a=x-ready, b=y-ready, out=add-xy);
	And16(a=x-ready, b=y-ready, out=and-xy);
	Mux16(a=and-xy, b=add-xy, sel=f, out=mux-f);
	
	// negate, out f and sign
	Not16(in=mux-f, out=n-mux-f);
	Mux16(a=mux-f, b=n-mux-f, sel=no, out=out, out[15]=ng, out[0..7]=p2, out[8..15]=p1);
	
	// Calc zero
	Or8Way(in=p1, out=or-p1);
	Or8Way(in=p2, out=or-p2);
	Or(a=or-p1, b=or-p2, out=or-out);
	Not(in=or-out, out=zr);
}